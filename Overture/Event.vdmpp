class Event

	instance variables
	  public startTime : Utils`Date;
	  public endTime : Utils`Date;
	  public name : Utils`String;
	  public stage: FashionShow`Stage;
	  public workers : set of (Person);
	  public news: set of (Notification);
	  
	  inv Utils`getDatePosition(startTime,endTime) = <Before>;
	  
	operations
	 --Constructor
	 	public Event: Utils`String * FashionShow`Stage * Utils`Date * Utils`Date ==> Event
		Event(name1, stage1, startD1, endD1) ==
		(name := name1; stage := stage1; startTime := startD1; endTime := endD1; workers := {}; news := {}; return self);
		
		--Add Worker
		public addWorker: Person ==> ()
		addWorker(w) == (workers:= workers union {w})
		pre w not in set workers			--PRE: Worker not in workers already
		post w in set workers;				--POS: New worker was added
		
		--Add Notification
		public addNotification: Notification ==> ()
		addNotification(notification) == (news := news union {notification});
		
		public updateWorkers: set of Person ==> ()
		updateWorkers(workers_) == (workers := workers_);
		
		--Reserve Seat
		public reserveSeat: () ==> ()
		reserveSeat() == (stage.reserved := stage.reserved + 1)
		pre stage.reserved < stage.seats						--PRE: must have free seats
		post stage.reserved = stage~.reserved + 1;	--POS: reserved seats was inc
		
		--Free Seat
		public freeSeat: () ==> ()
		freeSeat() == (stage.reserved := stage.reserved - 1)
		pre stage.reserved > 0											--PRE: must have reserved seats
		post stage.reserved + 1 = stage~.reserved;	--POS: reserved seats was dec
	
	functions
		--Overlap
		public static eventsOverlap: Event * Event -> bool
		eventsOverlap(e1,e2) == (
			if(e1.stage = e2.stage)
				then Utils`datesOverlap(e1.startTime,e1.endTime,e2.startTime,e2.endTime)
			else false
		);

end Event
