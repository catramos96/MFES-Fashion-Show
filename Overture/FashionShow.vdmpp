class FashionShow
 types
 	public Stage:: seats:nat1
					name: seq of (char);
 
 instance variables
 	public theme : Utils`String;
  public address : Utils`String;
  public startTime : Utils`Date;
  public endTime : Utils`Date;
  public stages: set of (Stage);
  public events : set of (Event);
	public ticketsLeft : map Ticket`TicketType to nat;
	public workers : set of (Person);
  
  inv forall e in set events & e.stage in set stages;		-- events in stages belonging to the show
  inv Utils`getDatePosition(startTime,endTime) = <Before>;-- start time < end time
  
 operations
 	--Constructor
 	public FashionShow: Utils`String * Utils`String * Utils`Date * Utils`Date ==> FashionShow
  FashionShow(theme_, address_, startTime_, endTime_) ==
  (theme := theme_; address := address_; startTime := startTime_; endTime := endTime_; return self );
  
  --Add Event
  public addEvent: Event ==> ()
  addEvent(e) == (events := events union {e})
  pre (not exists e1 in set events & Event`eventsOverlap(e1,e) = true) and		--PRE: Events don't overlap
  		Utils`getDatePosition(e.startTime,startTime) <> <Before> and 							--PRE: New event between show time
  		Utils`getDatePosition(e.endTime,endTime) <> <After>
  post e in set events;																												--POS: New event in events
  
  --Add Stage
  public addStage: Stage ==> ()
  addStage(s) == (stages:= stages union {s})
  post s in set stages;																												--POS: New stage in stages
  
  --BuyTicket
  public buyTicket: Person * Ticket`TicketType ==> Ticket
  buyTicket(attendee, type) == (
  	dcl left : nat := ticketsLeft(type) - 1,
  	t :Ticket := new Ticket(type,attendee, self);
  	
  	ticketsLeft := ticketsLeft ++ {type |-> left};
  	
  	attendee.addTicket(t);
  	return t;
  )
  pre ticketsLeft(type) > 0														--PRE: Has tickets 
  post ticketsLeft(type) + 1 = ticketsLeft~(type);		--POS: Dec ticket
  
  --Add Worker
  public addWorker: Person * Event * Person`TaskType ==> ()
  addWorker(w,e,t) == (
  workers := workers union {w};
  w.addJob(self,e,t);
  e.addWorker(w)
  )
  pre Person`isWorkingFunction(e.startTime,e.endTime,w.jobs) = false and	--PRE: Person has time to work
  		e in set events																											--PRE: New event belongs to the events of the show
  post w in set workers and w in set e.workers;														--POS: New worker in the show and event workers
  
 

end FashionShow
