class ShowsManager

instance variables
	public shows: set of (FashionShow) := {};
	inv not exists s1,s2 in set shows & s1<>s2 and 
	FashionShow`equals(s1,s2) and 																			--INV: Can't have the same show (same theme)
	Utils`datesOverlap(s1.startTime,s1.endTime,s2.startTime,s2.endTime);-- with overlapping dates
	
operations
	--Constructor
	public ShowsManager: () ==> ShowsManager
	ShowsManager() == (return self);
	
	--Add Show
	public addShow: FashionShow ==> ()
	addShow(fs) == (shows := shows union {fs})
	pre fs not in set shows and not exists s in set shows &
	FashionShow`equals(s,fs) and Utils`datesOverlap(s.startTime,s.endTime,fs.startTime,fs.endTime)
	post fs in set shows;										--POS: New show successfully added
	
	--Remove Show
	public removeShow: Utils`String ==> ()
	removeShow(theme) == (shows := shows \ {iota x in set shows & x.theme = theme})
	pre exists s in set shows & s.theme = theme					--PRE: Show in the seq of shows
	post not exists	s in set shows & s.theme = theme;		--POS: Show was removed
	
	--Get Spaces
	public getSpaces: () ==> set of Space
	getSpaces() == (
		dcl spaces : set of Space := {};
		for all s in set shows do(
			if(s.space not in set spaces) then spaces := spaces union {s.space}
		);
		return spaces
	)
	post forall s in set RESULT & 
		forall show in set shows & show in set s.reservations;	--POS: Spaces used by one more shows
	
	--Reset
	public reset: () ==> ()
	reset() == shows := {};
	
end ShowsManager